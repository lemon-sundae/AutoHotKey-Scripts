#Requires AutoHotkey v2.0
#SingleInstance Force
; #NoTrayIcon
Persistent true
/*
This script is mainly used to swiftly switch between different keyboard layouts, mostly reproducing the experience on Mainland China versions of MacBook and Magic Keyboards.
*/
HOTKEY_MODIFIER := "!+" ; Hotkeys for certain keyboard should be set in Windows Settings.
EN := "0"               ; For simplicity,
SC := "1"               ; use the identical modifiers for all keyboard layouts;
JP := "2"               ; the value assigned here == the corresponding ORDER == corresponding KEY.
TC := "3"
KEYBOARD_COUNT := 4

SetCapsLockState false
isCapsPressed := false
SendInput HOTKEY_MODIFIER CurrentLang := SC ; Default keyboard

; Switch to EN when calling out "Command Palette" or other Spotlight-like apps
~^Space::
{
    global
    Sleep 100 ; Wait a bit to avoid conflict
    SendInput HOTKEY_MODIFIER CurrentLang := EN
}

RWin:: ;CapsLock key reallocated to RWin in keyboard driver to avoid strange behavior
{
    global
    if (KeyWait("RWin", "T0.5")) { ; Short press
        if (!isCapsPressed) {
            if (GetKeyState("CapsLock", "T")) {
                SetCapsLockState False
            }
            else {
                if (CurrentLang == SC) {
                    SendInput HOTKEY_MODIFIER CurrentLang := EN
                }
                else {
                    SendInput HOTKEY_MODIFIER CurrentLang := SC
                }
            }
        }
        isCapsPressed := false
    }
    else { ; Hold
        if (!isCapsPressed) {
            isCapsPressed := true
            SetCapsLockState !GetKeyState("CapsLock", "T")
        }
    }
}

~LWin & ~Space::
{
    global CurrentLang := Mod(CurrentLang + 1, KEYBOARD_COUNT) ; Update "CurrentLang" when keyboard switch with "Win(hold)+Space" detected
}
